: CR 10 EMIT ;
: TAB 9 EMIT ;
: SPACE 32 EMIT ;
: CMP 2DUP > ROT < - ;
: BIN 2 BASE ! ;
: DEC 10 BASE ! ;
: HEX 16 BASE ! ;
: . 0 .R SPACE ;
: U. 0 U.R SPACE ;
: LITERAL IMMEDIATE [ ' LIT , ] LIT , , ;
: IF IMMEDIATE [ ' 0BRANCH ] LITERAL , HERE @ 0 , ;
: THEN IMMEDIATE DUP HERE @ SWAP - /CELLS SWAP ! ;
: ELSE IMMEDIATE [ ' BRANCH ] LITERAL , HERE @ 0 , SWAP DUP HERE @ SWAP - /CELLS SWAP ! ;
: <=> 2DUP < IF -1 ELSE > IF 1 ELSE 0 THEN THEN ;
: CHAR WORD DROP C@ ;
: BEGIN IMMEDIATE HERE @ ;
: UNTIL IMMEDIATE [ ' 0BRANCH ] LITERAL , HERE @ - /CELLS , ;
: AGAIN IMMEDIATE [ ' BRANCH ] LITERAL , HERE @ - /CELLS , ;
: WHILE IMMEDIATE [ ' 0BRANCH ] LITERAL , HERE @ 0 , ;
: REPEAT IMMEDIATE [ ' BRANCH ] LITERAL , SWAP HERE @ - /CELLS , DUP HERE @ SWAP - /CELLS SWAP ! ;
: ALIGNED 3 + 3 INVERT AND ;
: ALIGN HERE @ ALIGNED HERE ! ;
: C, HERE @ C! 1 HERE +! ;                    
: ." IMMEDIATE
    [ ' LITSTRING ] LITERAL ,
    HERE @ 0 ,
    BEGIN
        34 KEY DUP ROT <>
    WHILE
        C,
        DUP 1 SWAP +!
    REPEAT
    2DROP
    ALIGN
    [ ' TELL ] LITERAL ,
;
: CONSTANT IMMEDIATE WORD CREATE DOCON , , ;
: VARIABLE IMMEDIATE WORD CREATE DOVAR , 0 , ;
: EXIT IMMEDIATE 0 , ;
